plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'net.mineles'
version = '1.0.0'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()

    maven { url = 'https://jitpack.io' }
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'

    implementation('org.spongepowered:configurate-yaml:4.1.2') {
        exclude(module: 'snakeyaml')
        exclude(module: 'checker-qual')
        exclude(module: 'annotations')
        exclude(module: 'guava')
        exclude(module: 'guice')
    }
    implementation('org.spongepowered:configurate-gson:4.1.2') {
        exclude(module: 'gson')
        exclude(module: 'checker-qual')
        exclude(module: 'annotations')
        exclude(module: 'guava')
        exclude(module: 'guice')
    }

    implementation 'org.mongodb:mongodb-driver-sync:4.10.2'
    implementation('redis.clients:jedis:4.4.3') {
        exclude(module: 'gson')
        exclude(module: 'json')
        exclude(module: 'slf4j-api')
    }

    implementation 'com.github.cryptomorin:XSeries:9.2.0'
    implementation 'co.aikar:acf-paper:0.5.1-SNAPSHOT'
    implementation 'co.aikar:acf-bungee:0.5.1-SNAPSHOT'
    implementation 'de.tr7zw:item-nbt-api-plugin:2.11.3'

    compileOnly 'net.md-5:bungeecord-api:1.20-R0.1'
    compileOnly 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.25'

    compileOnly 'com.comphenix.protocol:ProtocolLib:5.0.0-SNAPSHOT'
    compileOnly 'com.github.LoneDev6:API-ItemsAdder:3.2.5'
    compileOnly 'me.clip:placeholderapi:2.11.3'

    compileOnly 'org.jetbrains:annotations:24.0.0'
    compileOnly 'org.checkerframework:checker-qual:3.12.0'
}

shadowJar {
    archiveFileName = 'MinelesLibrary-' + project.version + '.jar'

    relocate 'org.spongepowered.configurate', project.group + '.libs.configurate'
    relocate 'io.leangen.geantyref', project.group + '.libs.geantyref'

    //relocate 'com.github.dockerjava', project.group + '.libs.dockerjava'
    //relocate 'reactor', project.group + '.libs.reactor'

    relocate 'com.mongodb', project.group + '.libs.mongodb'
    relocate 'org.bson', project.group + '.libs.bson'

    relocate 'redis.clients.jedis', project.group + '.libs.jedis'
    relocate 'org.apache.commons.pool2', project.group + '.libs.commons-pool2'

    relocate 'co.aikar.commands', project.group + '.libs.commands'
    relocate 'co.aikar.locales', project.group + '.libs.locales'

    relocate 'com.cryptomorin.xseries', project.group + '.libs.xseries'
    relocate 'de.tr7zw', project.group + '.libs.nbtapi'
}

artifacts {
    archives shadowJar
}

processResources {
    def props = [version: version]
    inputs.properties props
    expand props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.java
        }
    }
}

test {
    useJUnitPlatform()
}
